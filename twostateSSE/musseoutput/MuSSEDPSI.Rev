#########################################################################################################
#
# MuSSE for Polyploidy and Diploidization in Solanaceae
#
# authors: Rosana Zenil-Ferguson, Will Freyman, Jordan
#
#########################################################################################################

setOption("useScaling", "true")


NUM_STATES=3
#########################
# Read in the data
#########################
observed_phylogeny <- readTrees("/home/eeg/shared/twostatesse/mussefull/ploidysi.nex")[1]
data <- readCharacterDataDelimited("/home/eeg/shared/twostatesse/musse/ploidysi.csv",stateLabels=3,type="NaturalNumbers",delimiter=",",headers=TRUE)
# Get some useful variables from the data. We need these later on
taxa <- observed_phylogeny.taxa()

# set my move index
mvi = 0
mni = 0

H = 0.587405

####################
# Create the rates #
####################

### Specify a prior on the diversification and turnover rate

### Create the constant prior parameters of the diversification rates
## Number of surviving lineages is 220
rate_mean <- ln( ln(220/2.0) / observed_phylogeny.rootAge() )
rate_sd <- 2*H

for (i in 1:NUM_STATES) {

### Create a lognormal distributed variable for the diversification rate
log_speciation[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
log_speciation[i].setValue( rate_mean )
speciation[i] := exp( log_speciation[i] )
moves[++mvi] = mvSlide(log_speciation[i],delta=0.20,tune=true,weight=3.0)

### Create a lognormal distributed variable for the turnover rate
log_extinction[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
log_extinction[i].setValue( rate_mean )
extinction[i] := exp( log_extinction[i] )
moves[++mvi] = mvSlide(log_extinction[i],delta=0.20,tune=true,weight=3)

}


#########################################################
# Set up the transition rate matrix for observed states #
#########################################################
## I defined very loosely my gamma priors for rate  of polyploidy in the Q-mat
shape_pr := 0.5
rate_pr := 1
############### Alternative definition or rate parameter
# Each transition rate between observed states are drawn
# from an exponential distribution with a mean of 10
# character state transitions over the tree.
# rate_pr := observed_phylogeny.treeLength() / 10

### Rate parameters in the Q matrix that aren't zero
rate_12 ~ dnGamma(shape=shape_pr, rate=rate_pr) ### Polyploidization
rate_21 ~ dnGamma(shape=shape_pr, rate=rate_pr) ### Diploidization
rate_31 ~ dnGamma(shape=shape_pr, rate=rate_pr) ### SI to SC
rate_32 ~ dnGamma(shape=shape_pr, rate=rate_pr) ### SI to Polyploidy

moves[++mvi] = mvScale( rate_12, weight=2 )
moves[++mvi] = mvScale( rate_21, weight=2 )
moves[++mvi] = mvScale( rate_31, weight=2 )
moves[++mvi] = mvScale( rate_32, weight=2 )



# first initialize the matrix with all zeros:
for (i in 1:3) {
for (j in 1:3) {
q[i][j] := 0.0
}
}
q[1][2] := rate_12
q[2][1] := rate_21
q[3][1] := rate_31
q[3][2] := rate_32

# generate the actual matrix
rate_matrix := fnFreeK(q, rescaled=false, matrixExponentialMethod="scalingAndSquaring")

### Create a constant variable with the prior probabilities of each rate category at the root.
rate_category_prior ~ dnDirichlet( rep(1,NUM_STATES) )
moves[++mvi] = mvDirichletSimplex(rate_category_prior,tune=true,weight=2)

### Create the fix parameter for the age of the root set to the observed age
root <- observed_phylogeny.rootAge()

#### construct a variable for the tree drawn from a birth death process
timetree ~ dnCDBDP( rootAge = root,
speciationRates   = speciation,
extinctionRates   = extinction,
Q                 = rate_matrix,
pi                = rate_category_prior,
delta             = 1.0,
condition         = "time")

### clamp the model with the "observed" tree
timetree.clamp( observed_phylogeny )
timetree.clampCharData( data )

#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rate_matrix)

### set up the monitors that will output parameter values to file and screen /home/eeg/shared/twostatesse/mussefull
monitors[++mni] = mnFile(filename="output/MuSSE_ploidysi250K.trees", printgen=1, timetree)
monitors[++mni] = mnModel(filename="output/MuSSE_ploidysi250K.log", printgen=1)

monitors[++mni] = mnJointConditionalAncestralState(tree=timetree, cdbdp=timetree, type="NaturalNumbers", printgen=1, withTips=true, withStartStates=false, filename="output/anc_states_MuSSE_ploidysi250K.log")
monitors[++mni] = mnScreen(printgen=10, rate_12, speciation, extinction)
monitors[++mni] = mnScreen(printgen=10, rate_21, speciation, extinction)
monitors[++mni] = mnScreen(printgen=10, rate_31, speciation, extinction)
monitors[++mni] = mnScreen(printgen=10, rate_32, speciation, extinction)

################
# The Analysis #
################

### workspace mcmc
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, moveschedule="random")

### pre-burnin to tune the proposals
mymcmc.burnin(generations=100,tuningInterval=100)

### run the MCMC
mymcmc.run(generations=5000)

##############################
# Summarize ancestral states #
##############################


anc_states = readAncestralStateTrace("/home/eeg/shared/twostatesse/mussefull/output/anc_states_MuSSE_ploidysi250K.log")
anc_tree = ancestralStateTree(tree=observed_phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="/home/eeg/shared/twostatesse/mussefull/output/anc_states_summaryMuSSEploidysi250K.tree", burnin=0, summary_statistic="MAP", site=0)

q()

